// all latest versions as of 2022-04-27
buildscript {
    ext {
        ktor_version = '2.1.0'
        logback_version = '1.2.11'
        testcontainers_version = '1.17.3'
    }
}

plugins {
    id 'java'
    id 'com.github.ben-manes.versions' version "0.42.0"
    id 'maven-publish'
    id 'signing'
    id 'org.hibernate.build.maven-repo-auth' version '3.0.4'
    id 'com.adarshr.test-logger' version '3.2.0'
    id 'org.jetbrains.kotlin.jvm' version '1.7.10'
}

group 'io.newm'
version '2.0.0'

sourceCompatibility = JavaVersion.VERSION_17
targetCompatibility = JavaVersion.VERSION_17

repositories {
    mavenCentral()
}

dependencies {
    implementation "org.flywaydb:flyway-core:9.1.3"
    implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk8:1.7.10"
    runtimeOnly "ch.qos.logback:logback-classic:$logback_version"
    implementation "io.ktor:ktor-server-core:$ktor_version"
    testImplementation "io.ktor:ktor-server-test-host:$ktor_version"
    testImplementation "org.testcontainers:postgresql:$testcontainers_version"
    testImplementation "org.testcontainers:testcontainers:$testcontainers_version"
    testImplementation "org.testcontainers:junit-jupiter:$testcontainers_version"
    testImplementation "com.zaxxer:HikariCP:5.0.1"
    testImplementation "org.postgresql:postgresql:42.4.1"
    testImplementation "org.junit.jupiter:junit-jupiter-api:5.9.0"
    testImplementation "org.junit.jupiter:junit-jupiter-params:5.9.0"
    testRuntimeOnly "org.junit.jupiter:junit-jupiter-engine:5.9.0"
}

compileKotlin {
    kotlinOptions.jvmTarget = "17"
}
compileTestKotlin {
    kotlinOptions.jvmTarget = "17"
}

jar {
    archiveBaseName = "ktor-flyway-feature"
    archiveVersion = "2.0.0"
}

test {
    useJUnitPlatform()
}

task javadocJar(type: Jar) {
    archiveClassifier  = 'javadoc'
    from javadoc
}

task sourcesJar(type: Jar) {
    archiveClassifier = 'sources'
    from sourceSets.main.allSource
}

artifacts {
    archives javadocJar, sourcesJar
}

publishing {
    repositories {
        maven {
            name = "ossrh"
            def releasesRepoUrl = "https://s01.oss.sonatype.org/service/local/staging/deploy/maven2/"
            def snapshotsRepoUrl = "https://s01.oss.sonatype.org/content/repositories/snapshots/"
            url = project.hasProperty('release') ? releasesRepoUrl : snapshotsRepoUrl
        }
    }
    publications {
        mavenJava(MavenPublication) {
            artifact(sourcesJar) {
                classifier = 'sources'
            }
            artifact(javadocJar) {
                classifier = 'javadoc'
            }
            pom {
                groupId = 'io.newm'
                artifactId = 'ktor-flyway-feature'

                from components.kotlin

                name = 'Ktor Flyway feature'
                description = 'Ktor flyway feature'
                url = 'https://github.com/projectNEWM/ktor-flyway-feature'
                licenses {
                    license {
                        name = 'MIT'
                        url = 'https://raw.githubusercontent.com/projectNEWM/ktor-flyway-feature/master/LICENSE'
                    }
                }
                developers {
                    developer {
                        id = 'viartemev'
                        name = 'Vyacheslav Artemyev'
                        email = 'viartemev@gmail.com'
                    }
                    developer {
                        id = 'AndrewWestberg'
                        name = 'Andrew Westberg'
                        email = 'andrewwestberg@gmail.com'
                    }
                }
                scm {
                    connection = 'scm:git:git://github.com/projectNEWM/ktor-flyway-feature.git'
                    developerConnection = 'scm:git:ssh://github.com/projectNEWM/ktor-flyway-feature.git'
                    url = 'https://github.com/projectNEWM/ktor-flyway-feature'
                }
            }
        }
    }
}

signing {
    useGpgCmd()
    sign publishing.publications.mavenJava
}

model {
    tasks.publishMavenJavaPublicationToMavenLocal {
        dependsOn project.tasks.withType(Sign)
    }
    tasks.publishMavenJavaPublicationToOssrhRepository {
        dependsOn project.tasks.withType(Sign)
    }
}